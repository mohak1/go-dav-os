/* boot/boot.s
 * Kernel entry point + Multiboot header for GRUB.
 *
 * Flow (Multiboot context):
 * - Provide the Multiboot2 header so GRUB recognizes and loads this image.
 * - GRUB jumps to _start with EAX=0x36D76289 and EBX pointing to the multiboot
 *   info struct (per spec).
 * - We immediately disable interrupts (cli) because no IDT/PIC is set up yet.
 * - We set ESP to a known 16 KB stack in .bss, aligned to 16 bytes.
 * - We park the CPU in a HLT loop as a placeholder until real kernel init runs.
 */
.code32

/* ---------------------------
 * Multiboot2 header section
 * ---------------------------
 * Placed in its own section so GRUB can locate it in the first 32 KiB.
 */
.set MULTIBOOT_MAGIC, 0xE85250D6
.set MULTIBOOT_ARCH,  0
.set USER_VA_BASE,    0x40000000

.section .multiboot2
.align 8

multiboot_header_start:
	.long MULTIBOOT_MAGIC
	.long MULTIBOOT_ARCH
	.long multiboot_header_end - multiboot_header_start
	.long -(MULTIBOOT_MAGIC + MULTIBOOT_ARCH + (multiboot_header_end - multiboot_header_start))

# Info request tag: memory map (6), ELF sections (9)
	.align 8
	.word 1
	.word 0
	.long 16
	.long 6
	.long 9

# Entry address tag
	.align 8
	.word 3
	.word 0
	.long 16
	.long _start
	.long 0

/* End tag */
	.align 8
	.word 0
	.word 0
	.long 8

multiboot_header_end:

// --- Stack ---
.section .bootstrap_stack, "aw", @nobits

.align 16
stack_bottom:
	.skip 16384              # 16 KB di stack
stack_top:

.align 16
.global tss
tss:
	.skip 104

# Long mode paging structures (4 KiB aligned, zero-initialized).
.align 4096
pml4:
	.skip 4096
.align 4096
pdpt:
	.skip 4096
.align 4096
pd0:
	.skip 4096
.align 4096
pd1:
	.skip 4096
.align 4096
pd2:
	.skip 4096
.align 4096
pd3:
	.skip 4096
.align 4096
pt_user:
	.skip 4096

.align 4096
user_stack_page:
	.skip 4096
.global __bootstrap_end
__bootstrap_end:

.align 4
multiboot_info_ptr:
	.long 0

.section .data
.align 8
.global gdt64
gdt64:
	.quad 0x0000000000000000
	.quad 0x00AF9A000000FFFF
	.quad 0x00CF92000000FFFF
	.quad 0x00AFFA000000FFFF  # Index 3: User Code (0x18 | 3 = 0x1B)
	.quad 0x00CFF2000000FFFF  # Index 4: User Data (0x20 | 3 = 0x23)
	.quad 0  # TSS low
	.quad 0  # TSS high

gdt64_desc:
	.word (gdt64_end - gdt64 - 1)
	.long gdt64
	.long 0

gdt64_end:

/* ---------------------------
 * Executable code
 * ---------------------------
 * GRUB jumps here after validating the header, with:
 * - EAX = 0x2BADB002 (Multiboot magic passed to the kernel)
 * - EBX = pointer to the Multiboot info structure
 */
	.section .text
	.global  _start
	.type    _start, @function

_start:
	cli # disable interrupts (no IDT/PIC set yet)

# initialize ESP to the top of our 16 KB stack in .bss
	mov  $stack_top, %esp

# Multiboot2: EBX contains the address of the multiboot info structure.
	movl %ebx, multiboot_info_ptr

	call setup_long_mode

	cli

.Lhang:
	jmp .Lhang
.size _start, . - _start

setup_long_mode:
# Build identity-mapped paging (4 GiB via 2 MiB pages).
# Kernel mappings stay supervisor-only (U/S=0).
	lea pml4, %edi
	movl $pdpt, %eax
	orl $0x07, %eax
	movl %eax, (%edi)
	movl $0, 4(%edi)

	lea pdpt, %edi
	movl $pd0, %eax
	orl $0x03, %eax
	movl %eax, (%edi)
	movl $0, 4(%edi)
	movl $pd1, %eax
	orl $0x07, %eax
	movl %eax, 8(%edi)
	movl $0, 12(%edi)
	movl $pd2, %eax
	orl $0x03, %eax
	movl %eax, 16(%edi)
	movl $0, 20(%edi)
	movl $pd3, %eax
	orl $0x03, %eax
	movl %eax, 24(%edi)
	movl $0, 28(%edi)

	movl $0x83, %edx           # present|rw|ps (supervisor)

	lea pd0, %edi
	xorl %ecx, %ecx
	movl $0x00000000, %ebx

.Lmap_2m_pd0:
	movl %ecx, %eax
	shll $21, %eax             # ecx * 2 MiB
	addl %ebx, %eax
	orl %edx, %eax
	movl %eax, (%edi)
	movl $0, 4(%edi)
	addl $8, %edi
	incl %ecx
	cmpl $512, %ecx
	jne .Lmap_2m_pd0

	lea pd1, %edi
	xorl %ecx, %ecx
	movl $0x40000000, %ebx

.Lmap_2m_pd1:
	movl %ecx, %eax
	shll $21, %eax
	addl %ebx, %eax
	orl %edx, %eax
	movl %eax, (%edi)
	movl $0, 4(%edi)
	addl $8, %edi
	incl %ecx
	cmpl $512, %ecx
	jne .Lmap_2m_pd1

	lea pd2, %edi
	xorl %ecx, %ecx
	movl $0x80000000, %ebx

.Lmap_2m_pd2:
	movl %ecx, %eax
	shll $21, %eax
	addl %ebx, %eax
	orl %edx, %eax
	movl %eax, (%edi)
	movl $0, 4(%edi)
	addl $8, %edi
	incl %ecx
	cmpl $512, %ecx
	jne .Lmap_2m_pd2

	lea pd3, %edi
	xorl %ecx, %ecx
	movl $0xC0000000, %ebx

.Lmap_2m_pd3:
	movl %ecx, %eax
	shll $21, %eax
	addl %ebx, %eax
	orl %edx, %eax
	movl %eax, (%edi)
	movl $0, 4(%edi)
	addl $8, %edi
	incl %ecx
	cmpl $512, %ecx
	jne .Lmap_2m_pd3

# User virtual window:
#   0x40000000 -> user program page
#   0x40001000 -> user stack page
# PDPT[1] already points to pd1 with U/S=1. Replace pd1[0] with a PT.
	lea pd1, %edi
	movl $pt_user, %eax
	orl $0x07, %eax            # present|rw|user
	movl %eax, (%edi)
	movl $0, 4(%edi)

	# Keep the old 2 MiB identity map semantics via 4 KiB PTEs:
	# map 0x40000000..0x401FFFFF as supervisor identity first.
	lea pt_user, %edi
	xorl %ecx, %ecx
	movl $0x40000000, %ebx

.Lmap_4k_pt_user_identity:
	movl %ecx, %eax
	shll $12, %eax             # ecx * 4 KiB
	addl %ebx, %eax
	orl $0x03, %eax            # present|rw (supervisor)
	movl %eax, (%edi)
	movl $0, 4(%edi)
	addl $8, %edi
	incl %ecx
	cmpl $512, %ecx
	jne .Lmap_4k_pt_user_identity

	# Override first two pages as user mappings.
	movl $__user_program_page, %eax
	andl $0xFFFFF000, %eax
	orl $0x05, %eax            # present|user (read-only)
	movl %eax, pt_user
	movl $0, pt_user+4

	movl $user_stack_page, %eax
	andl $0xFFFFF000, %eax
	orl $0x07, %eax            # present|rw|user
	movl %eax, pt_user+8
	movl $0, pt_user+12

# Load PML4 and enable PAE.
	movl $pml4, %eax
	movl %eax, %cr3

	movl %cr4, %eax
	orl  $0x20, %eax
	movl %eax, %cr4

# Enable long mode in EFER.
	movl $0xC0000080, %ecx
	rdmsr
	orl  $0x100, %eax
	wrmsr

# Load GDT and enable paging.
	lgdt gdt64_desc
	movl %cr0, %eax
	orl  $0x80000000, %eax
	movl %eax, %cr0

# Far jump to 64-bit code segment.
	ljmp $0x08, $long_mode_entry

.code64
long_mode_entry:
	movw $0x10, %ax
	movw %ax, %ds
	movw %ax, %es
	movw %ax, %ss
	movw %ax, %fs
	movw %ax, %gs

	movq $stack_top, %rsp
	andq $-16, %rsp
	subq $8, %rsp

# Clear BSS.
	movq $__bss_start, %rdi
	movq $__bss_end, %rcx
	subq %rdi, %rcx
	xor %eax, %eax
	rep stosb

	movl multiboot_info_ptr(%rip), %edi
	call go_0kernel.Main

.Lhang64:
	hlt
	jmp .Lhang64

# void go_0kernel.TriggerSysWrite(uint64 buf, uint64 n)
.global go_0kernel.TriggerSysWrite
.type   go_0kernel.TriggerSysWrite, @function

go_0kernel.TriggerSysWrite:
    mov  %rdi, %rcx      # buf
    mov  %rsi, %rdx      # n
    mov  $1, %eax        # SYS_WRITE
    mov  $1, %ebx        # fd=1
    int  $0x80
    ret
.size go_0kernel.TriggerSysWrite, . - go_0kernel.TriggerSysWrite

# void go_0kernel.TriggerSysExit(uint32 status)
.global go_0kernel.TriggerSysExit
.type   go_0kernel.TriggerSysExit, @function

go_0kernel.TriggerSysExit:
    mov  %edi, %ebx      # status
    mov  $2, %eax        # SYS_EXIT
    int  $0x80
    ret
.size go_0kernel.TriggerSysExit, . - go_0kernel.TriggerSysExit

# uint64 go_0kernel.TriggerSysGetTicks()
.global go_0kernel.TriggerSysGetTicks
.type   go_0kernel.TriggerSysGetTicks, @function

go_0kernel.TriggerSysGetTicks:
    mov  $3, %eax        # SYS_GETTICKS
    int  $0x80
    ret
.size go_0kernel.TriggerSysGetTicks, . - go_0kernel.TriggerSysGetTicks
